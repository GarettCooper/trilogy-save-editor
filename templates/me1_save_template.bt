
// Bool
typedef struct {
	uint value: 1;
    uint : 31;
} bool <read=read_bool>;

string read_bool(bool &b) {
    if(b.value == 1)
        return "true";
    else
        return "false";
}

// String
typedef struct {
	int len <bgcolor=0x000077>;
    SetBackColor(0x000055 + len);

	// DÃ©tection utf8
	if (len < 0) {
		wchar_t chars[Abs(len)];
	}
	else {
		char chars[len];
	}
} String <read=read_string>;

string read_string(String &s) {
    if(exists(s.chars))
        return s.chars;
    else
        return "<empty>";
}

// Name
typedef struct {
	String str;
	FSkip(8);
} Name <read=read_name>;

string read_name(Name &n) {
    return read_string(n.str);
}

// Import
struct Import {
    int index_package_name;
    FSkip(4);
    int index_class_name;
    FSkip(4);
    int index_link;
    int index_object_name;
    FSkip(4);
};

// Export
struct Export {
    int class_name_id;
    FSkip(4);
    int link_id;
    int object_name_id;
    int index_archtype_name;
    FSkip(8);
    int flag;
    int data_size;
    int data_offset;
    FSkip(32);
};

// Data
struct Data {
    int begin;
};

// SaveGame
struct {
    FSeek(0x8);
	int offset <format=hex, bgcolor=0x00aa55>;
    FSeek(offset);
    struct {
        uint magic <format=hex>;
        uint16 version;
        uint16 licence_version;
        int data_offset <format=hex>;
        String upx_name;
        uint flags <format=hex>;
        int names_len;
        int names_offset <format=hex>;
        int exports_len;
        int exports_offset <format=hex>;
        int imports_len;
        int imports_offset <format=hex>;
        int no_mans_land_offset <format=hex>;
        FSkip(16);
        int generations <format=hex>;
        FSkip(36 + generations * 12);
        int compression;
    } header <bgcolor=0x8855ff>;
    struct {
        FSeek(header.names_offset);
	    Name name[header.names_len] <optimize=false>;
    } names;
    struct {
        FSeek(header.imports_offset);
	    Import import[header.imports_len] <optimize=false>;
    } imports <bgcolor=0x88ff55>;
    struct {
        FSeek(header.exports_offset);
	    Export export[header.exports_len] <optimize=false>;
    } exports <bgcolor=0xff8855>;
    struct {
        FSeek(header.no_mans_land_offset);
        byte something[header.data_offset - header.no_mans_land_offset];
    } no_mans_land <bgcolor=0x887766>;
    struct {
        FSeek(header.data_offset);
        local int i;
        for (i = 0; i < header.exports_len; i++) {
            FSeek(exports.export[i].data_offset);
            Data data;
        }
    } datas <bgcolor=0x447799>;

} MassEffect1 <open=true>;